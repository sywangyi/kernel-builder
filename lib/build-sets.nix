{
  nixpkgs,
  system,
  torchVersions,
}:

let
  inherit (nixpkgs) lib;

  overlay = import ../overlay.nix;

  inherit (import ./torch-version-utils.nix { inherit lib; })
    backend
    flattenSystems
    ;

  # All build configurations supported by Torch.
  buildConfigs =
    system:
    let
      filterMap = f: xs: builtins.filter (x: x != null) (builtins.map f xs);
      systemBuildConfigs = filterMap (version: if version.system == system then version else null) (
        flattenSystems torchVersions
      );
    in
    builtins.map (buildConfig: buildConfig // { backend = backend buildConfig; }) systemBuildConfigs;

  flattenVersion = version: lib.replaceStrings [ "." ] [ "_" ] (lib.versions.pad 2 version);

  overlayForTorchVersion = torchVersion: sourceBuild: self: super: {
    pythonPackagesExtensions = super.pythonPackagesExtensions ++ [
      (
        python-self: python-super: with python-self; {
          torch =
            if sourceBuild then
              python-self."torch_${flattenVersion torchVersion}"
            else
              python-self."torch-bin_${flattenVersion torchVersion}";
        }
      )
    ];
  };

  # An overlay that overides CUDA to the given version.
  overlayForCudaVersion = cudaVersion: self: super: {
    cudaPackages = super."cudaPackages_${flattenVersion cudaVersion}";
  };

  overlayForRocmVersion = rocmVersion: self: super: {
    rocmPackages = super."rocmPackages_${flattenVersion rocmVersion}";
  };

  overlayForXpuVersion = xpuVersion: self: super: {
    xpuPackages = super."xpuPackages_${flattenVersion xpuVersion}";
  };

  backendConfig = {
    cpu = {
      allowUnfree = true;
    };

    cuda = {
      allowUnfree = true;
      cudaSupport = true;
    };

    metal = {
      allowUnfree = true;
      metalSupport = true;
    };

    rocm = {
      allowUnfree = true;
      rocmSupport = true;
    };

    xpu = {
      allowUnfree = true;
      xpuSupport = true;
    };
  };

  xpuConfig = {
    allowUnfree = true;
    xpuSupport = true;
  };

  # Construct the nixpkgs package set for the given versions.
  mkBuildSet =
    buildConfig@{
      backend,
      cpu ? false,
      cudaVersion ? null,
      metal ? false,
      rocmVersion ? null,
      xpuVersion ? null,
      torchVersion,
      system,
      bundleBuild ? false,
      sourceBuild ? false,
    }:
    let
      backendOverlay =
        if buildConfig.backend == "cpu" then
          [ ]
        else if buildConfig.backend == "cuda" then
          [ (overlayForCudaVersion buildConfig.cudaVersion) ]
        else if buildConfig.backend == "rocm" then
          [ (overlayForRocmVersion buildConfig.rocmVersion) ]
        else if buildConfig.backend == "metal" then
          [ ]
        else if buildConfig.backend == "xpu" then
          [ (overlayForXpuVersion buildConfig.xpuVersion) ]
        else
          throw "No compute framework set in Torch version";
      config =
        backendConfig.${buildConfig.backend} or (throw "No backend config for ${buildConfig.backend}");

      pkgs = import nixpkgs {
        inherit config system;
        overlays = [
          overlay
        ]
        ++ backendOverlay
        ++ [ (overlayForTorchVersion torchVersion sourceBuild) ];
      };

      torch = pkgs.python3.pkgs.torch;

      extension = pkgs.callPackage ./torch-extension { inherit torch; };
    in
    {
      inherit
        buildConfig
        extension
        pkgs
        torch
        bundleBuild
        ;
    };
in
map mkBuildSet (buildConfigs system)
